WHITESPACE =
    _{ " " // Space
    | "\u{09}" // Tab
    | "\u{0A}" // New Line
    | "\u{0D}" // Carriage Return
    | "\u{0C}" // Form Feed
    | "\u{0B}" // Vertical Tab
    }

COMMENT =
    _{ "//" ~ comment_to_eol*
    | "/*" ~ multiline_comment_content* ~ "*/"
    }

comment_to_eol = _{ !NEWLINE ~ ANY }
multiline_comment_content = _{ !"*/" ~ ANY }

escaped_string = ${ "\"" ~ escaped_string_char* ~ "\"" }
escaped_string_char =
    ${ escaped_backslash_or_quote
    | hex_escape
    | other_string_char
    }
escaped_backslash_or_quote = @{ "\\\\" | "\\\"" }
hex_escape =
    @{ "\\u" ~ hexd ~ hexd ~ hexd ~ hexd
    | "\\U" ~ hexd ~ hexd ~ hexd ~ hexd ~ hexd ~ hexd ~ hexd ~ hexd
    }
other_string_char = @{ (!("\"" | "\\") ~ ANY) }

identifier =
    @{ ('A'..'Z' | 'a'..'z' | "_")
    ~ ('A'..'Z' | 'a'..'z' | '0'..'9' | "_")*
    }

number = { ASCII_DIGIT+ }
hexd = { ASCII_HEX_DIGIT }

ggrulebook = { SOI ~ ruledef+ ~ EOI }
ruledef = { paramrule | ggrule | metacommand }

paramrule = { identifier ~ memoization? ~ "{" ~ identifier ~ ("," ~ identifier)* ~ "}" ~ ":" ~ ggproduction ~ ";" }
ggrule = { identifier ~ memoization? ~ ":" ~ ggproduction ~ ";" }
ggproduction = { alternative ~ ("|" ~ alternative)* }
memoization = { "@" }
metacommand = { "#" ~ metacommand_definition ~ ";" }

alternative = { condition* ~ weight? ~ sequence_elem+ }

condition = { "!" ~ negated? ~ identifier }
negated = { "!" }
weight = { "<" ~ number ~ ">" }

sequence_elem = { single_sequence_elem ~ kleene? }
kleene = { "*" | "+" }

single_sequence_elem =
    { parenthesized
    | optional
    | call_params
    | identifier
    | escaped_string
    }

parenthesized = { "(" ~ ggproduction ~ ")" }
optional = { "[" ~ weight? ~ ggproduction ~ "]" }
call_params = { identifier ~ "{" ~ ggproduction ~ ("," ~ ggproduction)* ~ "}" }

metacommand_definition =
    { randomize_condition_metacommand
    }
randomize_condition_metacommand = { "randomize_condition" ~ identifier }
